plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.jaredsburrows.license'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        if (keystoreProperties.containsKey('storeFile')) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    defaultConfig {
        applicationId "com.omelan.cofi"
        minSdkVersion 25
        targetSdkVersion 34
        compileSdk 34
        versionCode 182
        versionName "1.21.8"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        manifestPlaceholders = [profileable: false]
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig keystoreProperties.containsKey('storeFile') ?
                    signingConfigs.release : signingConfigs.debug
        }

        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        benchmark {
            signingConfig signingConfigs.debug
            minifyEnabled true
            manifestPlaceholders["profileable"] = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".benchmark"
            versionNameSuffix "-benchmark"
        }
    }

    flavorDimensions.add("version")
    productFlavors {
        instant {
            dimension "version"
            versionCode android.defaultConfig.versionCode - 1
            versionNameSuffix "-instant"
            minSdkVersion 26
        }
        playStore {
            dimension "version"
        }
        full {
            dimension "version"
            versionCode android.defaultConfig.versionCode
        }
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_19
        targetCompatibility JavaVersion.VERSION_19
    }

    kotlin {
        jvmToolchain(19)
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.8"
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES',
                         'META-INF/LICENSE',
                         'META-INF/LICENSE.txt',
                         'META-INF/license.txt',
                         'META-INF/NOTICE',
                         'META-INF/NOTICE.txt',
                         'META-INF/notice.txt',
                         'META-INF/ASL2.0',
                         'META-INF/AL2.0',
                         'META-INF/LGPL2.1',
                         'META-INF/*.kotlin_module']
        }
    }


    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    namespace 'com.omelan.cofi'

}

licenseReport {
    generateCsvReport = false
    generateHtmlReport = false
    generateJsonReport = true
    // These options are ignored for Java projects
    copyHtmlReportToAssets = false
    copyHtmlReportToAssets = false
    copyJsonReportToAssets = true
}

configurations {
    ktlint
}

dependencies {
    def camerax_version = "1.3.0"

    // CameraX 核心库
    implementation "androidx.camera:camera-core:${camerax_version}"
    // Camera2 实现
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    // 使用新版本的 ML Kit 依赖
    implementation 'com.google.mlkit:barcode-scanning:17.2.0'

    implementation project(path: ':cofi-share')
    implementation 'androidx.compose.ui:ui-graphics-android:1.6.8'
    implementation 'androidx.compose.animation:animation-core-android:1.6.8'
    implementation 'androidx.camera:camera-view:1.4.0'
    implementation 'com.google.mlkit:vision-common:17.3.0'
    implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.1'
    implementation 'androidx.camera:camera-lifecycle:1.4.0'
    ktlint("com.pinterest:ktlint:0.48.2") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
    implementation platform("androidx.compose:compose-bom:$compose_bom_version")

    instantImplementation('com.google.android.gms:play-services-instantapps:18.1.0')
    playStoreImplementation 'com.google.android.gms:play-services-wearable:18.2.0'
    playStoreImplementation "androidx.wear:wear-remote-interactions:1.0.0"
    playStoreImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.7.3'

    // Tooling support (Previews, etc.)
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"

    // Material Design
    implementation "androidx.compose.material3:material3:1.3.0-beta04"
    implementation "androidx.compose.material3:material3-window-size-class"

    implementation 'com.github.KieronQuinn:MonetCompat:0.2.1'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    // Navigation
    implementation "androidx.navigation:navigation-compose:2.8.0-beta05"

    // Integration with observables
    implementation "androidx.compose.runtime:runtime-livedata"
    // Jetpack Utils
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.30.1"
    implementation "androidx.compose.animation:animation-graphics"

    implementation "androidx.activity:activity-compose:1.9.0"

    // UI Tests
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.6.8"
    testImplementation 'junit:junit:4.13.2'
    androidTestUtil 'androidx.test:orchestrator:1.5.0'

    // QR code
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'

    // Baidu map
    implementation 'com.baidu.lbsyun:BaiduMapSDK_Search:7.6.3'
    implementation 'com.baidu.lbsyun:BaiduMapSDK_Map-BWNavi:7.6.3'
    implementation 'com.baidu.lbsyun:BaiduMapSDK_Location:9.6.4'
    implementation 'com.baidu.lbsyun:BaiduMapSDK_Util:7.6.3'
    // 添加 OkHttp 依赖
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
}

tasks.register("ktlintCheck", JavaExec) {
    group = "verification"
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass = "com.pinterest.ktlint.Main"
    // see https://pinterest.github.io/ktlint/install/cli/#command-line-usage for more information
    args "src/**/*.kt", "**.kts", "!**/build/**"
}

tasks.named("check") {
    dependsOn tasks.named("ktlintCheck")
}

tasks.register("ktlintFormat", JavaExec) {
    group = "formatting"
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass = "com.pinterest.ktlint.Main"
    jvmArgs "--add-opens=java.base/java.lang=ALL-UNNAMED"
    // see https://pinterest.github.io/ktlint/install/cli/#command-line-usage for more information
    args "-F", "src/**/*.kt", "**.kts", "!**/build/**"
}

